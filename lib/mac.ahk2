; MIT License
; Copyright (c) 2021 Kosaki Mezumona
#UseHook
#include "commands.ahk2"

class MacSimulator
{
	static Instance[]
	{
		get
		{
			static instance := MacSimulator.New()
			return instance
		}
	}

	IgnoredWindowMatcher[]
	{
		get
		{
			if (!this._ignoredWindowMatcher)
			{
				this._ignoredWindowMatcher := WindowMatcher.New()
			}
			return this._ignoredWindowMatcher
		}
		set
		{
			this._ignoredWindowMatcher := value
		}
	}

	__New()
	{
		this._keyDown := false
		this._taskSwitching := false
		this._ignoredWindowMatcher := ""
	}

	RegisterKeybinds(commandKey)
	{
		HotIf((*) => this.IsAbleToOverrideKeys())

		HotKey("*" . commandKey, (*) => this.OnCommandKeyDown())
		HotKey("*" . commandKey . " Up", (*) => this.OnCommandKeyUp())
		HotKey("Tab", (*) => this.OnTabKeyDown())
		HotKey("*+vkDB", (*) => this.OnShiftOpenBracketDown())
		HotKey("*+vkDD", (*) => this.OnShiftCloseBracketDown())

		HotIf()
	}

	IsAbleToOverrideKeys() => !(this._ignoredWindowMatcher && this._ignoredWindowMatcher.MatchActive())

	OnCommandKeyDown()
	{
		if (!this._keyDown)
		{
			Send("{Blind}{RCtrl Down}")
			this._keyDown := true
		}
	}

	OnCommandKeyUp()
	{
		if (this._keyDown)
		{
			if (this._taskSwitching)
			{
				Send("{Blind}{RAlt Up}")
			}
			else
			{
				Send("{Blind}{RCtrl Up}")
			}
		}
		this._keyDown := false
		this._taskSwitching := false
	}

	OnTabKeyDown()
	{
		if (this._keyDown && !this._taskSwitching)
		{
			Send("{Blind}{RCtrl Up}{RAlt Down}")
			this._taskSwitching := true
		}
		Send("{Blind}{Tab}")
	}

	OnShiftOpenBracketDown()
	{
		if (this._keyDown)
		{
			Send("{Shift Down}{Tab}{Shift Up}")
		}
		else
		{
			Send("{Blind}{vkDB}")
		}
	}

	OnShiftCloseBracketDown()
	{
		if (this._keyDown)
		{
			Send("{Ctrl}{Tab}")
		}
		else
		{
			Send("{Blind}{vkDD}")
		}
	}
}
