; MIT License
; Copyright (c) 2021 Kosaki Mezumona
#UseHook
#include "cmd.ahk2"
#include "window.ahk2"

class MacSimulator
{
	static Instance[args*]
	{
		get
		{
			static instance := MacSimulator()
			return instance
		}
	}

	Command[args*]
	{
		get => this._command
		set => this._command := value
	}

	IgnoredWindowMatcher[args*]
	{
		get => this._ignoredWindowMatcher
		set => this._ignoredWindowMatcher := value
	}

	__New()
	{
		this._commandKeyDown := false
		this._taskSwitching := false
		this._ignoredWindowMatcher := WindowMatcher()
	}

	RegisterKeybinds(commandKey)
	{
		HotIf((*) => this.IsAbleToOverrideKeys())

		HotKey("*" . commandKey, (*) => this.OnCommandKeyDown())
		HotKey("*" . commandKey . " Up", (*) => this.OnCommandKeyUp())
		HotKey("Tab", (*) => this.OnTabKeyDown())
		HotKey("*+vkDB", (*) => this.OnShiftOpenBracketDown())
		HotKey("*+vkDD", (*) => this.OnShiftCloseBracketDown())
		HotKey("*+vk33", (*) => this.OnShiftThreeDown())
		HotKey("*+vk34", (*) => this.OnShiftFourDown())
		HotKey("*+vk35", (*) => this.OnShiftFiveDown())
		HotKey("vkE2", (*) => Send("_"))
		HotKey("<^Up", (*) => Send("{RWin Down}{Tab}{RWin Up}"))
		HotKey("<^Left", (*) => Send("{Blind}{RWin Down}{Left}{RWin Up}"))
		HotKey("<^Right", (*) => Send("{Blind}{RWin Down}{Right}{RWin Up}"))

		HotIf()
	}

	IsAbleToOverrideKeys() => !this._ignoredWindowMatcher.MatchActive()

	OnCommandKeyDown()
	{
		if (!this._commandKeyDown)
		{
			Send("{Blind}{RCtrl Down}")
			this._commandKeyDown := true
		}
	}

	OnCommandKeyUp()
	{
		Send("{Blind}{RAlt Up}")
		Send("{Blind}{RCtrl Up}")
		this._commandKeyDown := false
		this._taskSwitching := false
	}

	OnTabKeyDown()
	{
		if (this._commandKeyDown && !this._taskSwitching)
		{
			Send("{Blind}{RCtrl Up}{RAlt Down}")
			this._taskSwitching := true
		}
		Send("{Blind}{Tab}")
	}

	OnShiftOpenBracketDown()
	{
		if (this._commandKeyDown)
		{
			this._command.SwitchPrevTab()
		}
		else
		{
			Send("{Blind}{vkDB}")
		}
	}

	OnShiftCloseBracketDown()
	{
		if (this._commandKeyDown)
		{
			this._command.SwitchNextTab()
		}
		else
		{
			Send("{Blind}{vkDD}")
		}
	}

	OnShiftThreeDown()
	{
		if (this._commandKeyDown)
		{
			this._command.CaptureScreen()
		}
		else
		{
			Send("{Blind}{vk33}")
		}
	}

	OnShiftFourDown()
	{
		if (this._commandKeyDown)
		{
			this._command.CaptureActiveWindow()
		}
		else
		{
			Send("{Blind}{vk34}")
		}
	}

	OnShiftFiveDown()
	{
		if (this._commandKeyDown)
		{
			this._command.CaptureWithScreenshotTools()
		}
		else
		{
			Send("{Blind}{vk35}")
		}
	}
}
